#pragma kernel HandRegionDetectFromPalm
#pragma kernel HandRegionDetectFromPose
#pragma kernel HandCrop
#pragma kernel HandsKeyPointPost

#include "HandRegion.hlsl"
#include "LowPassFilter.hlsl"
#include "Misc.hlsl"
#include "Packages/jp.keijiro.mediapipe.blazepalm/Shader/Struct.hlsl"

#define HAND_KEYPOINT_COUNT 21


ByteAddressBuffer _detectionCount;
float _regionDetectDt;
StructuredBuffer<float4> _poseInput;
StructuredBuffer<PalmDetection> _palmDetections;
RWStructuredBuffer<HandRegion> _handsRegionFromPalm;

[numthreads(1, 1, 1)]
void HandRegionDetectFromPalm(uint id : SV_DispatchThreadID)
{
    uint detectionCount = _detectionCount.Load(0);
    if(detectionCount == 0) return;

    HandRegion pRegionL = _handsRegionFromPalm[0];
    HandRegion pRegionR = _handsRegionFromPalm[1];

    float4 index_l = _poseInput[19];
    float4 pinky_l = _poseInput[17];
    float2 boundingBoxCenter_l = (2.0f * index_l.xy + pinky_l.xy) / 3.0f;

    float4 index_r = _poseInput[20];
    float4 pinky_r = _poseInput[18];
    float2 boundingBoxCenter_r = (2.0f * index_r.xy + pinky_r.xy) / 3.0f;
    
    for(uint i = 0; i < detectionCount; i++){
        // Input from the palm detection model
        const PalmDetection palm = _palmDetections[i];

        // Palm region size (squarified and enlarged bounding box)
        float size = max(palm.extent.x, palm.extent.y) * 3.05f;

        // Palm angle
        const float2 up = palm.keyPoints[3] - palm.keyPoints[0];
        float angle = atan2(up.y, up.x) - PI / 2;

        const float4 box = float4(palm.center, size, angle);

        float distanceFromL = distance(box.xy, boundingBoxCenter_l.xy);
        float distanceFromR = distance(box.xy, boundingBoxCenter_r.xy);

        uint isRight = distanceFromL > distanceFromR;
        HandRegion region = pRegionL;
        if(isRight) region = pRegionR;

        // Low pass filter parameters and input vector
        const float3 lpf_params = float3(2, 1.5f, _regionDetectDt);
        region.dBox = lpf_Step_dx(box, region.box, region.dBox, lpf_params);
        region.box = lpf_Step_x(box, region.box, region.dBox, lpf_params);

        // Region crop matrix update
        float4x4 m1 = makeTranslationMatrix(region.box.xy - region.box.z / 2);
        float4x4 m2 = makeScalingMatrix(region.box.z);
        float4x4 m3 = makeTranslationMatrix(0.5);
        float4x4 m4 = makeRotationMatrix(region.box.w);
        float4x4 m5 = makeTranslationMatrix(-0.5 + float2(0, 0.05f));
        region.cropMatrix = mul(mul(mul(mul(m1, m2), m3), m4), m5);

        // Compute buffer update
        _handsRegionFromPalm[isRight] = region;
    }
}


int _isRight;
float _bboxDt;
RWStructuredBuffer<HandRegion> _bboxRegion;

[numthreads(1,1,1)]
void HandRegionDetectFromPose(uint3 id : SV_DispatchThreadID)
{
    float4 wrist = _poseInput[15 + _isRight];
    float4 index = _poseInput[19 + _isRight];
    float4 pinky = _poseInput[17 + _isRight];
    // Estimate middle finger.
    float2 middle = (2.0f * index.xy + pinky.xy) / 3.0f;
    // Bounding box size as double distance from middle finger to wrist.
    float boxSize = 2.0f * sqrt((middle.x - wrist.x) * (middle.x - wrist.x) + (middle.y - wrist.y) * (middle.y - wrist.y));

    // bounding box
    float2 boundingBoxCenter = middle;
    float boundingBoxSize = boxSize * 1.5f;

    // bounding box angle
    float target = PI * 0.5;
    float2 up = middle - wrist.xy;
    float boundingBoxAngle = target - atan2(-up.y, up.x);

    const float4 box = float4(boundingBoxCenter, boundingBoxSize, boundingBoxAngle);
    
    HandRegion region = _bboxRegion[0];

    // Low pass filter parameters and input vector
    const float3 lpf_params = float3(2, 1.5f, _bboxDt);
    region.dBox = lpf_Step_dx(box, region.box, region.dBox, lpf_params);
    region.box = lpf_Step_x(box, region.box, region.dBox, lpf_params);

    // Region crop matrix update
    float4x4 m1 = makeTranslationMatrix(region.box.xy - region.box.z / 2);
    float4x4 m2 = makeScalingMatrix(region.box.z);
    float4x4 m3 = makeTranslationMatrix(0.5);
    float4x4 m4 = makeRotationMatrix(region.box.w);
    float4x4 m5 = makeTranslationMatrix(-0.5);
    region.cropMatrix = mul(mul(mul(mul(m1, m2), m3), m4), m5);

    // Compute buffer update
    _bboxRegion[0] = region;
}


float2 _spadScale;
int _isVerticalFlip;
int _handRegionIndex;

int _handCropImageSize;
sampler2D _handCropInput;
StructuredBuffer<HandRegion> _handCropRegion;
RWStructuredBuffer<float> _handCropOutput;

[numthreads(8, 8, 1)]
void HandCrop(uint2 id : SV_DispatchThreadID)
{
    float4x4 xform = _handCropRegion[_handRegionIndex].cropMatrix;

    // UV coordinates
    float2 uv = (id + 0.5) / _handCropImageSize;
    if(_isVerticalFlip) uv.y = 1 - uv.y;
    uv = mul(xform, float4(uv, 0, 1)).xy;
    uv = (uv - 0.5) * _spadScale + 0.5;

    // UV gradients
    float2 duv_dx = mul(xform, float4(1.0 / _handCropImageSize, 0, 0, 0)).xy;
    float2 duv_dy = mul(xform, float4(0, -1.0 / _handCropImageSize, 0, 0)).xy;

    // Texture sample
    float3 rgb = tex2Dgrad(_handCropInput, uv, duv_dx, duv_dy).rgb;

    uint offs = (id.y * _handCropImageSize + id.x) * 3;
    _handCropOutput[offs + 0] = rgb.r;
    _handCropOutput[offs + 1] = rgb.g;
    _handCropOutput[offs + 2] = rgb.b;
}


float _handPostDt;
StructuredBuffer<float4> _handPostInput;
StructuredBuffer<HandRegion> _handPostRegion;
RWStructuredBuffer<float4> _handPostOutput;
RWStructuredBuffer<float4> _handPostDeltaOutput;

[numthreads(HAND_KEYPOINT_COUNT + 1, 1, 1)]
void HandsKeyPointPost(uint id : SV_DispatchThreadID)
{
    if(id == HAND_KEYPOINT_COUNT){
        // Set hand score and handedness.
        _handPostOutput[HAND_KEYPOINT_COUNT] = _handPostInput[0];
        return;
    }

    HandRegion region = _handPostRegion[_handRegionIndex];

    float3 x = _handPostInput[id + 1].xyz;
    if(!_isVerticalFlip) x.y = 1.0f - x.y;
    float3 p_x = _handPostOutput[id].xyz;
    float3 p_dx = _handPostDeltaOutput[id].xyz;

    // Map to cordinates of input texture from landmark.
    x = mul(region.cropMatrix, float4(x, 1)).xyz;
    x.xy = (x.xy - 0.5) * _spadScale + 0.5;

    // Low pass filter
    float3 lpf_params = float3(30, 1.5, _handPostDt);
    float3 dx = lpf_Step_dx(x, p_x, p_dx, lpf_params);
    x = lpf_Step_x(x, p_x, dx, lpf_params);

    _handPostOutput[id] = float4(x, 1);
    _handPostDeltaOutput[id] = float4(dx, 1);
}
